; ГРС Кушнаренково (Дюртюлинское ЛПУМГ)
; 12.2019 Галеев
; Аварийный останов

; v2  -  Добавлены диагностические параметры для контроля выполнения алгоритма
; dout[1] - команда АО
; dout[2] - ход выполнения 0-нет , 1-выполняется
; dout[3] - причина сработки
; aout[4] - код ошибки выполнения
; aout[5] - дата последнего выполнения
; aout[6] - дата окончания выполнения


#include "eval.lib\valtrack.evl"
#include "eval.lib\set.evl"
#include "eval.lib\front.evl"


;----------- Условия выполнения аварийного останова -----------------------------
  ; по команде с экрана АРМ или от Диспетчера
  ; При нажатой более 4 секунд кнопке(механической)- только при аварийной ситуации:
        ; - Аварийно-высокое давление
        ; - Пожар в операторной
        ; - Пожар в блоке переключения (при наличии пож.сигнализации)
        ; - Пожар в блоке одоризации (при наличии пож.сигнализации)
;-------------------------------------------------------------------------------
func checkFire(dummy)
  x=0
  x=x|{ПОЖАР ОПЕ КУШН} ;<Пожар в операторной>.
  x=x|{ПОЖАР ПЕР КУШН} ;<Пожар в блоке переключения>.
  ;x=x|{ПОЖАР ОДО КУШН} ;<Пожар в блоке одоризации>.
  return(x)
endfunc


func checkPrecond(dummy)
  x=0
  if(ne({РЕЖИМ ГРС КУШН},0))
    x=x+{КОМ АВОСТ КУШН}  ;1 команда - без условий
    if (eq(valTrack({КН АВОСТ КУШН},4,8),1))    ; кнопка - только при аварийной ситуации
      x=x+2*checkFire(0)        ;2 Пожар
      x=x+3*{РВЫХ123АВ КУШН}    ;3 Аварийно-высокое давление
    endif
  endif
  return(x)
endfunc
;--------------------------------------------------------------------------------

func oninit(t)
 dout[1]=0
 dout[2]=0
 dout[3]=0
 aout[4]=0
 aout[5]=true({ДАТА АО КУШН})
 aout[6]=true({ДАТА ЗАО КУШН})

 ; ждем первого опроса модулей
 sleep(10*18)
endfunc

reason=checkPrecond(0)
if ne(reason,0)

   dout[2]=1	; ход ао
   dout[3]=reason
   aout[5]=time()

   ; закрыть охранный кран
   x=setwex({КРАН ОХР КУШН}[sys_num],1,30)

   sleep(18)
   if ne({КРАН ОХР КУШН},2)
     ; закрыть входной кран
     x=setwex({КРАН ВХОД КУШН}[sys_num],1,20)
   endif

   ; закрыть байпасный кран
   x=setwex({КР БАЙП КУШН}[sys_num],1,20)

   ; закрыть выходной
   x=setwex({КРАН ВЫХ КУШН}[sys_num],1,20)

   ; подогреватель отключить
   x=set_wait({ПГ УПР КУШН}[sys_num],2,20)

   ; отключить одоризатор
   x=set_wait({РЕЖ ОДОР КУШН}[sys_num],0,20)

   ; Если пожар
   if (checkFire(0))

     ; если закрыты : Охранный, байпасный, выходной краны
     ;if(eq({КРАН ОХР КУШН},2)&eq({КРАН ВЫХ КУШН},2)&eq({КР БАЙП КУШН},2))
       ; открыть свечные краны
       ;x=setwex({КР СВ НИЗ КУШН}[sys_num],0,30)
       ;x=setwex({КР СВ ВЫС КУШН}[sys_num],0,30)
     ;endif

     ; если охранный кран не закрыт, а закрыты: входной, байпасный, выходной краны
     ;if(((ne({КРАН ОХР КУШН},2))&eq({КРАН ВХОД КУШН},2))&eq({КРАН ВЫХ КУШН},2))
     if(eq({КРАН ВХОД КУШН},2)&eq({КРАН ВЫХ КУШН},2))
       ; открыть свечной кран с низ стороны
       x=setwex({КР СВНИЗ КУШН}[sys_num],0,30)
     endif
   endif

   ; переводим грс в режим по месту
   set {КОМ РЕЖ3}[sys_num],1

   sleep(5*18)
   dout[1]=0	; ком ао (возм причина)
   dout[2]=0

   aout[6]=time()

endif

if front(ne({РЕЖИМ ГРС КУШН},0),9)
  dout[3]=0
endif
